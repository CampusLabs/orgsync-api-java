import groovy.text.SimpleTemplateEngine

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'me.champeau.gradle:jbake-gradle-plugin:0.2'
        classpath 'org.pegdown:pegdown:1.4.2'
    }
}

def siteDir = 'site'
def siteBuildDir = "$buildDir/site"
def siteOutDir = "$buildDir/jbaked"

task copySite(type: Copy) {
    from(file(siteDir))
    into(siteBuildDir)
}

def pagesBinding = [
    version: version,

    // A list of older versions we want to link
    versions: [
        '0.2.0'
    ]]

task convertPages(dependsOn: copySite) << {
    def engine = new SimpleTemplateEngine()

    new File('site/pages').listFiles(
        {dir, file-> file ==~ /.*?\.gsp/ } as FilenameFilter
    ).toList().each { file ->
        def outName = file.name.replace('.gsp', '')
        def outFile = new File(siteBuildDir, "content/$outName")
        def t = engine.createTemplate(file).make(pagesBinding)
        t.writeTo(new FileWriter(outFile))
    }
}

jbake {
    input = file(siteBuildDir)
    output = file(siteOutDir)
    clearCache = true
}

jbake.dependsOn(convertPages)
